/*
 * Copyright (C) 2022 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id("com.android.library")
}

android{
    compileSdkVersion gradle.ext.aaosLatestSDK
    defaultConfig{
        minSdkVersion 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true', coverage: 'true'
    }
    namespace "com.android.car.media.testmediaapp"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest{
            java.srcDirs += 'tests/instrumenttests/src'
        }

        test {
            java.srcDirs += 'tests/unittests/src'
        }
    }

    signingConfigs {
        aaos {
            storeFile file(gradle.ext.aaosCertPath)
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }

        platform {
            if (file(gradle.ext.platformCertPath).exists()) {
                storeFile file(gradle.ext.platformCertPath)
                storePassword 'android'
                keyAlias 'androiddebugkey'
                keyPassword 'android'
            }
        }
    }

    flavorDimensions "signingkey"
    productFlavors {
        aaos {
            dimension "signingkey"
            signingConfig signingConfigs.aaos
        }
        platform {
            dimension "signingkey"
            signingConfig signingConfigs.platform
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled = true
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.media:media:1.6.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.car.app:app:1.1.0'

    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.10.3'
    testImplementation "org.mockito:mockito-core:2.19.0"
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    implementation project(":car-media-extensions")
}